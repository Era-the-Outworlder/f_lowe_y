
//     __  __                                                        
//    / / / /   _____  ___    _____   ____   ____ _   ____ ___   ___ 
//   / / / /   / ___/ / _ \  / ___/  / __ \ / __ `/  / __ `__ \ / _ \
//  / /_/ /   (__  ) /  __/ / /     / / / // /_/ /  / / / / / //  __/
//  \____/   /____/  \___/ /_/     /_/ /_/ \__,_/  /_/ /_/ /_/ \___/ 
//                                                                   


//-------- OnTeachName --------
//This is called from the config menu in the main character's double click menu when the user says they want to change their name.

OnTeachName	
{
	if reference[0] == "menu" //function was called from User Config
		{	
			"\c\t"
			--
			"\0\s[3]%(Narrator)\n[300] * Change var_player_name? \![open,inputbox,OnNameTeach,-1]\0\s[7]"
			
			"\0\s[8]%(Flowey) * RE-RE-RE-RE-RE-\_w[500]\c%(Flowey) * Name the Fallen Human \![open,inputbox,OnNameTeach,-1]\0\s[7]"
			
			"\0\s[16]%(Flowey) * Human name name name fallen human fallen fallen... name? \![open,inputbox,OnNameTeach,-1]"
		}
	else
		{
			"\c\t"
			--
			"\0\s[3]%(Flowey)* Name. Name. \n\_w[500]\s[7] * Name. Fallen Human. Fallen... name? \![open,inputbox,OnNameTeach,-1]\x\s[0]"
			
			"\0\s[3]%(Narrator)\n[300]\f[align,center] Name the Fallen Human\_w[500]\0\s[8]\![open,inputbox,OnNameTeach,-1]\x\s[0]"
		}
}


//--OnNameTeach
//This is where the user inputs their name and the ghost analyzes it.

OnNameTeach
{
	_tempname = TOSTR(reference[0]) //this stores the name value from reference[0] in tempname
	_selfname = TOSTR(sakuraname)
	
	if _tempname == _selfname
	{
		"\0\s[21]%(Flowey) * Chose. Chose. Chose. Chose. Chose\n[200]\_w[500]Chose... already...\![open,inputbox,OnNameTeach,-1]"
	}
	elseif reference[0] == "flowey" || reference[0] == "Flowey" || reference[0] == "FLOWEY"
	{
		"\0\s[21]%(Flowey) * Chose... already...\![open,inputbox,OnNameTeach,-1]"
		
	}
	elseif reference[0] == "FlOwEy" || reference[0] == "fLoWeY"
	{
		"\0\s[21]%(Flowey) * Clevvvvvvvver Vvve ver very clever.\![open,inputbox,OnNameTeach,-1]"
		"\0\s[18]%(Narrator) * (You decide that may have been a bad idea.)\![open,inputbox,OnNameTeach,-1]"
	}
	else
	{
		reference[0] = TOLOWER(reference[0]) //changes it to lowercase
		reference[0] = REPLACE(reference[0]," ","") //removes spaces
		reference[0] = REPLACE(reference[0],".","") //removes periods
		reference[0] = REPLACE(reference[0],"-","") //removes dashes
		reference[0] = REPLACE(reference[0],"_","") //removes underscores
		
		
		if reference[0] == "flowey" 
		{
			"\0\s[21]%(Flowey) * Chose... already...\![open,inputbox,OnTeachName,-1]"
		}
		elseif reference[0] == "" //This is if the user doesn't put in a name at all, or only enters invalid characters.
		{
			username = "NULL" //this sets the user's name to NULL, which is a fun joke but isn't exactly safe. AFAIK, NULL isn't used as a value in YAYA, but it could cause problems if the ghost share's the user's name with outside programs through a plugin or something. 
		
			"\0\s[29]%(Flowey) * var_player_name = %(username)\n[200]"
			"\0\s[29]%(Narrator) * var_player_name = %(username) Is this correct? \n[200]"
			--
			
			"\![*] \q[Change it.,OnTeachName]\n \n[-100]\f[align,right]\![*] \q[It's fine.,OnRisk]"
		}
		elseif reference[0] == "sans"
		{
			if month == 2 && day == 15
			{
				username = "Sanstrosity"
				"\0\s[29]%(Flowey) * IT-IT-IT-IT-IT-IT-IT-IT-IT-\n%(Narrator) * (Perhaps another name?)"
			}
			else
			{
			username = "Smiley Trashbag" //this sets the user's name to something insulting
		
			"\0\s[21]%(Flowey) * \![embed,OnGLITCH,trashbag] \0\s[3]\n"
			"\0\s[21]%(Narrator) * f_lowe_y isn't a fan of that name. \n (You get the feeling he may decide to call you something rude instead.) \n"
			}
			--
			"\![*] \q[Change it.,OnTeachName]\n \n[-100]\f[align,right]\![*] \q[Risk it.,OnRisk]"
		}
		elseif reference[0] == "papyrus"
		{
			if month == 2 && day == 15
			{
				username = "Papalgamate"
				
				"\0\s[29]%(Flowey) * %(HAPPY)\n[200]/
				\c%(Narrator) * IT... May be a bad idea to use that name. \n"
				--
				"\![*] \q[Change it.,OnTeachName]\n \n[-100]\f[align,right]\![*] \q[Risk it.,OnRisk]"
			}
			else
			{
				username = _tempname //this sets the user's name to what they entered
				_temp = OnGLITCH(username)
				"\0\s[23]%(Flowey) * %(_temp) \_w[500]\s[0]"
				"\0\s[23]%(Narrator) * It appears he has... fond? ...memories of someone. \_w[500]\0\s[0]"
			}
		}
		elseif reference[0] == "help" //template for referencing HELP_tale names
		{
			if month == 2 && day == 15
			{
				username = "THANK"
				"\0\s[29]%(Flowey) * %(HAPPY)\n[200]/
				\c%(Narrator) * IT... May be a bad idea to use that name. \n"
				--
				"\![*] \q[Change it.,OnTeachName]\n \n[-100]\f[align,right]\![*] \q[Risk it.,OnRisk]"
			}
			else
			{
				username = _tempname //this sets the user's name to what they entered
				_temp = OnGLITCH(username)
				"\0\s[23]%(Flowey) * %(_temp) \_w[500]\0\s[0]"
				"\0\s[23]%(Narrator) * It appears he has... [var_sentiment] ...memories of someone. \_w[500]\0\s[0]"
			}
		}
		elseif reference[0] == "name" //some character
		{
			username = "reference" 
		
			"\0\s[5]%(Flowey) * var_player_name = %(username)\n"
			
			"\0\s[5]%(Narrator) * var_player_name = %(username)\nIs this correct? \n"
			--
			
			"\![*] \q[Change it.,OnTeachName]\n \n[-100]\f[align,right]\![*] \q[It's fine.,OnRisk]\n"
		}
		elseif _tempname == username //checks against existing username
		{
			"\0\s[3]%(Flowey) * %(username)... Again... Title...?\_w[500]\n[150]"
			
			"\0\s[3]%(Narrator) * That is already your username. Are you trying to change something else? \_w[500]\n[150]"
			--
			
			"\![*] \q[Yes,titlechoose]\n \n[-100]\f[align,right]\![*] \q[No,titlenone]\n"
		}
		else //if the user put in something not specified above, ie a proper name the ghost can use. 
		{
			username = _tempname
			"\0\s[3]%(Flowey) * Title...?\_w[500]\n[150]"
			
			"\0\s[3]%(Narrator) * Do you want to be called by a title? \_w[500]\n[150]"
			--
			
			"\![*] \q[Yes,titlechoose]\n \n[-100]\f[align,right]\![*] \q[No,titlenone]\n"
		}
	}
}

// If the user chooses to risk a FUN name
OnRisk
{
	thatonename = username
	agreedToConsequences ,= "naming"
	if firstBoot == 1 //this checks for the flag that was set during the firstBoot sequence
	{
		"\0\s[16]%(Narrator) * f_lowe_y has learned to call you... something."
		--
		OnTeachPronouns
	}
	else
	{
		"\0\s[24]%(laughVFX) * HA HA HA HA HA HA HA- %(Narrator) * Whatever follows is a consequence of your decision."
	}	

}
	
Select.titlechoose
{		
	
	"\![*] \q[Mr.,titlemr]\n/
	\![*] \q[Ms.,titlems]\n/
	\![*] \q[Mrs.,titlemrs]\n/
	\![*] \q[Mx.,titlemx]\n/
	\![*] \q[-ssi,titlessi]\n/
	\![*] \q[-san,titlesan]\n/
	\![*] \q[No title,titlenone]"
}
	
Select.titlenone
{
		username = username
		NameDone
}

Select.titlemr
{
		username = "Mr. " + username
		NameDone
}
		
Select.titlems
{
		username = "Ms. " + username
		NameDone
}

Select.titlemrs
{
		username = "Mrs. " + username
		NameDone
}

Select.titlemx
{
		username = "Mx. " + username
		NameDone
}

Select.titlessi
{
		username = username + "-ssi"
		NameDone
}

Select.titlesan
{
		username = username + "-san"
		NameDone
}

//--NameDone
//Signifies that it's done, moves on to wherever else


NameDone
{
	if firstBoot == 1 //this checks for the flag that was set during the firstBoot sequence
	{
		"\0\s[28]%(Narrator) * f_lowe_y has learned your name, \_w[500]%(username).\0\s[3]\_w[500]\_s\b[-1]\c\_s"
		--
		"\![timerraise,1000,1,OnTeachPronouns]"
	}
	elseif firstBoot == 2
	{
		"\0\s[28]%(Narrator) * Your name has been recorded as \_w[500]%(username).\0\s[3]\_w[500]\_s\b[-1]\c\_s"
		--
		"\![timerraise,1000,1,OnTeachPronouns]"
	}
	else
	{
		"\0\s[16]%(Flowey) * \_w[500]%(username) \_w[500]%(username) \_w[500]%(username) \_w[500]%(username) \_w[500]%(username)!\e"
	}	
}
